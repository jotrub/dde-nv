###############################################################################
#
# NPSOL/source/createNumSysForConstraints
#
# revision history:
# 030423 changed messagnes in try..catch phrases
# 030318 written by mmo
#
###############################################################################
createNumSysForConstraints:= proc(
  Sys::NLP,
  WorkingDir::string,
  RestoreKeyword
)

  local OldDir, EAEsInstance, VisEAEs, ConstraintsAEsInstance;

  #
  # check for common errors
  #
    if not
      member(RestoreKeyword, {'new', 'restore'})
    then
      error("3rd input argument, RestoreKeyword, must be new or restore");
    end if;

    if not
      type(NumSys, `module`)
    then
      printf("reading module NumSys\n");
      read(cat(_EnvModulesDir, "/NumSys/source/NumSys.mpl")):
    end if;

    if not
      type(ADIFOR, `module`)
    then
      printf("reading module ADIFOR\n");
      read(cat(_EnvModulesDir, "/ADIFOR/source/ADIFOR.mpl")):
    end if;

    if not
      Aux:-FileOperations:-dirExists(WorkingDir)
    then
      system(cat("mkdir ", WorkingDir));
    end if; 

  OldDir:= currentdir();

  #
  # create external EAEs
  #
    try
      EAEsInstance:= NumSys:-EAE:-CreateInstance(
        Sys,
        WorkingDir,
        RestoreKeyword,
        'NoInterface' 
      );
    catch:
      printf("error while creating instance of NumSys for EAEs:\n");
      error;
    finally
      currentdir(OldDir);
    end try;

  #
  # create external AEs
  #
    VisEAEs:= map(lhs, Sys[ExplicitAEs]);
    try
      ConstraintsAEsInstance:= NumSys:-AE:-CreateInstance(
        Sys,
        WorkingDir,
        RestoreKeyword,
        'NoInterface', # suppress building of shared object
        cat(WorkingDir, "/NumSys-EAE-CreateInstanceForStandardEAEs"),
        VisEAEs
      );
    catch:
      printf("in CreateNumSysForConstraints, error while calling NumSys:-AE:-CreateInstance:\n"); 
      error;
    finally
      currentdir(OldDir);
    end try; 

  #
  # call adifor
  #
    try
      currentdir(WorkingDir); 
      ADIFOR:-RunAD(
        ConstraintsAEsInstance:-GetFileList(),
        'y',
        nops(Sys[Variables]),
        'res'
      ):
    catch:
      printf("in CreateNumSysForConstraints, error while calling ADIFOR:-RunAD:\n"); 
      error; 
    finally
      currentdir(OldDir);
    end try;

  return(ConstraintsAEsInstance); 

end proc:
