###############################################################################
#
# @path NPSOL/source/SimpleWrapperTemplate/setParameters
#
# @revision
# 021217 now accesses local module variable Params, parameters can now
#   be changed after instanciation
# 021209 written by mmo
#
###############################################################################
setParameters:= proc(
  NewPars::list(name= EvalsToFloat)
)

  local NewValues, NewParsNames, Missing, Obsolete, i1, c, D; 

  #
  # make sure values for all parameters are given in 
  # first input argument
  #
    NewParsNames:= map(lhs, NewPars); 
    Missing, Obsolete:= Aux:-ListOperations:-getMissingAndObsoleteNames(
      NewParsNames,
      ParNames
    );
    if not
      Missing= {}
    then
      error("assignments are missing for %1", Missing);
    end if;

  #
  # assign values
  #   
    NewValues:= evalf(subs(
      NewPars,
      ParNames
    ));

  #
  # scale if necessary
  #
    if
      ScalingParsOn= true
    then
      D:= ScalingPars:-getD();
      c:= ScalingPars:-getc();

      for i1 from 1 to NumParams do
        Params[i1]:= D[i1]* NewValues[i1]- c[i1];
      end do;

      return();

    else

      for i1 from 1 to NumParams do
        Params[i1]:= NewValues[i1];
      end do;

      return();

    end if; 

end proc; 

