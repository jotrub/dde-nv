#############################################################
#
# @path Aux/SystemClasses/listOfErrorsInDDESysPart2.txt
#
# @brief Generate 'strict' list of errors in DDESys 
#
# @param table of type DDESys
#
# @return list of errors found in aDDESys
#
# @notes (i) Entreis "DelVars" should be the same order as "DynVars" (=n)
#        (ii) The class should be extended to allow "m*n" 
#             diffenent entries for "DynVars" 
#
# @authors dka
#
# @revision
# 2013-02-19 Created by dka
#
#############################################################
listOfErrorsInDDESysPart2:= proc(
  aDDESys
)

  local ListOfErrors, LHSidesOfODEs, DynVarsFromODEs, i,
    ParsAndIndependents, NumberOfExplicitAEs, SetOfValidExpr, i1,
    ListOfUndefExpr, ObsolExpr, Integrators, item, ParAndVar,
    ParAndExplAE, ParAndDelVar, ExplVarsAndOtherVars1, 
    ExplVarsAndOtherVars2, ExplVarsAndOtherVars3; 


  ListOfErrors:= []:

  #----------------------------------------------------------
  #
  # Check whether left hand sides in DAESys["ODEs"]
  # are derivatives of DAESys["Independents"] and
  # compare
  #
  #----------------------------------------------------------
    #
    # DynVars must be the same and in same order as lhs of ODEs
    #
      LHSidesOfODEs:= map(lhs, aDDESys["ODEs"]);
      DynVarsFromODEs:= map(Aux:-ListOperations:-getNameFromDerivSymbol, LHSidesOfODEs);  
  
      #
      # compare strings to avoid problems with differences between
      # names, symbols, indexed
      #
        DynVarsFromODEs:= map(convert, DynVarsFromODEs, string); 
  
      if not
        DynVarsFromODEs= map(convert, aDDESys["DynVars"], string)
      then
        ListOfErrors:= [
          op(ListOfErrors),
          "DynVars must be in same order as lhs of ODEs"
        ]; 
      end if; 

  #----------------------------------------------------------
  #
  # Check whether left hand sides in DAESys["AEs"]
  # are zeors and compare number of AEs and algebraic vars
  #
  #----------------------------------------------------------
    #
    # compare number of algebraic vars to number of AEs
    #
      if not(nops(aDDESys["AEs"])= nops(aDDESys["AlgVars"])) then
        ListOfErrors:= [op(ListOfErrors),
          `number of algebraic variables is not equal to number of AEs`
        ];
      end if; 

    #
    # check whether lhs of equations is equal to zero
    #
      for i from 1 to nops(aDDESys["AEs"]) do
        if not (lhs(aDDESys["AEs"][i]) = 0) then
  	ListOfErrors:= [op(ListOfErrors),
  	  `left hand sides of AEs must be ZERO`
  	];
        end if;
      end do;    # end loop over DAESys["AEs"][i]

  #----------------------------------------------------------
  #
  # Check whether aDDESys["ExplicitAEs"] are well defined:
  #
  # Are indeterminants in aDDESys["ExplicetAEs"][i1] either
  # independent variable, parameter, or defined in previous
  # equations aDDESys["ExplicitAEs"][1..(i1- 1)]?
  #
  #----------------------------------------------------------

      ParsAndIndependents:=
        Aux:-ListOperations:-getSetOfValidExprIn(aDDESys["Parameters"]) union
        Aux:-ListOperations:-getSetOfValidExprIn(aDDESys["AlgVars"]) union
        Aux:-ListOperations:-getSetOfValidExprIn(aDDESys["DynVars"]) union
        Aux:-ListOperations:-getSetOfValidExprIn(aDDESys["DelVars"]);

      NumberOfExplicitAEs:= nops(aDDESys["ExplicitAEs"]);

      SetOfValidExpr:= ParsAndIndependents;

      #
      # loop over DAESys["ExplicitAEs"][i1]
      #

      for i1 from 1 to NumberOfExplicitAEs do
        
         #
         # check whether aDDESys["ExplicitAEs"][i1] is well
         # defined
         #
                
         ListOfUndefExpr:= Aux:-ListOperations:-getListOfUndefExprIn(
           aDDESys["ExplicitAEs"][i1],
           convert(SetOfValidExpr, list)
         );
        
         #
         # append errors to list, add a comment
         #
        
         if not(ListOfUndefExpr= []) then
           ListOfErrors:= [op(ListOfErrors),
             cat(
               ListOfUndefExpr,
               ` in equation `,
               aDDESys["ExplicitAEs"][i1],
               ` of ExplicitAEs undefined.`
             )
           ];
         end if; 
        
         #
         # add lhs of aDDESys["ExplicitAEs"][i1] to
         # SetOfValidExpr
         #
        
         SetOfValidExpr:= SetOfValidExpr union
           {op(1, aDDESys["ExplicitAEs"][i1])}
        
      end do;  #end loop over DAESys["ExplicitAEs"][i1]
        
  #----------------------------------------------------------
  #
  # Check whether aDDESys["ODEs"] are well defined:
  #
  # Are indeterminants in aDDESys["ODEs"] either
  # independent variables, parameters, or defined in
  # aDDESys["ExplicitAEs"] and aDDESys["DelVars"]?
  #
  # Note that SetOfValidExpr comprises DynVars,
  # Parameters and lhs of ExplicitAEs after execution
  # of preceding block.
  #----------------------------------------------------------

      #
      # loop over DAESys["ODEs"][1..i1]
      #

      for i1 from 1 to nops(aDDESys["ODEs"]) do

        #
        # check whether aDDESys["ODEs"][i1] is well
        # defined
        #
       
        ListOfUndefExpr:= Aux:-ListOperations:-getListOfUndefExprIn(
          aDDESys["ODEs"][i1],
          convert(SetOfValidExpr, list)
        );
       
        #
        # append errors to list, add a comment
        #
       
        if not(ListOfUndefExpr= []) then
          ListOfErrors:= [op(ListOfErrors),
            cat(
              ListOfUndefExpr,
              ` in equation `,
              aDDESys["ODEs"][i1],
              ` of ODEs undefined.`
            )
          ];
        end if; 

      end do;  #end loop over DAESys["ODEs"][i1]

  #----------------------------------------------------------
  #
  # Check whether aDDESys["AEs"] are well defined:
  #
  # Are indeterminants in aDDESys["AEs"] either
  # algebraic variables, parameters, or defined in
  #  aDDESys["ExplicitAEs"] and aDDESys["DelVars"]?
  #
  # Note that SetOfValidExpr comprises AlgVars,
  # Parameters and lhs of AEs after execution
  # of preceding block.
  #----------------------------------------------------------

      #
      # loop over DAESys["AEs"][1..i1]
      #

      for i1 from 1 to nops(aDDESys["AEs"]) do

        #
        # check whether aDDESys["AEs"][i1] is well
        # defined
        #
       
        ListOfUndefExpr:= Aux:-ListOperations:-getListOfUndefExprIn(
          aDDESys["AEs"][i1],
          convert(SetOfValidExpr, list)
        );
       
        #
        # append errors to list, add a comment
        #
       
        if not(ListOfUndefExpr= []) then
          ListOfErrors:= [op(ListOfErrors),
            cat(
              ListOfUndefExpr,
              ` in equation `,
              aDDESys["AEs"][i1],
              ` of AEs undefined.`
            )
          ];
        end if; 

      end do;  #end loop over DAESys["AEs"][i1]

  #--------------------------------------------------
  #
  # check for obsolete expressions in aDDESys
  #
  #--------------------------------------------------
    #
    # note that x will be an obsolete expression if only
    # `x'` but not `x` occurs
    #
      ObsolExpr:=Aux:-ListOperations:-getObsolExprInDDESys(aDDESys);
      #
      # check if ObsoleteExpr comprises variables `x` which
      # occur as `x'` on lhs of ODEs but not as `x` on rhs of ODEs
      # or on rhs of AEs
      #
        Integrators:= [];
        for item in ObsolExpr do
          if 
            member(item, aDDESys["DynVars"])
          then
            Integrators:= [op(Integrators), item];
          end if;
        end do;
      #
      # remove Integrators from ObsolExpr and warn about
      # existence of Integrators separately
      #
        if not
          Integrators= []
        then 
          ObsolExpr:= Aux:-ListOperations:-removeItemFromList(Integrators, ObsolExpr);
          WARNING(
            "in ListOfErrorsInDAESys, the following variables only occur as derivatives but not as variables: %1", 
            op(Integrators));
        end if; 

    if
      not(ObsolExpr=[])
    then
      WARNING("in ListOfErrorsInDAESys, %1 is an obsolete expression", op(ObsolExpr));
    end if; 

  #----------------------------------------------------------
  #
  # check if lists Parameters and DynVars have common entries
  #
  #----------------------------------------------------------
    ParAndVar:=map(
      has,
      aDDESys["Parameters"],
      aDDESys["DynVars"]
    );

    if
      (member(true, ParAndVar, 'i2'))
    then
      ListOfErrors := [op(ListOfErrors),
     cat(
       aDDESys["Parameters"][i2],
       ` is declared both parameter and variable `
     )
      ];
    end if; 

  #------------------------------------------------------------
  #
  # lists Parameters and lhs in ExplicitAEs must not have common
  #   entries
  #
  #------------------------------------------------------------
    ParAndExplAE:= map(
      has,
      aDDESys["Parameters"],
      map(lhs, aDDESys["ExplicitAEs"])
    );

    if
      (member(true, ParAndExplAE, 'i2'))
    then
      ListOfErrors := [op(ListOfErrors),
     cat(
       aDDESys["Parameters"][i2],
       ` occurs both in Parameters and lhs of ExplicitAEs `
     )
      ];
    end if; 


  #----------------------------------------------------------
  #
  # check if lists Parameters and DelVars have common entries
  #
  #----------------------------------------------------------
    ParAndDelVar:=map(
      has,
      aDDESys["Parameters"],
      aDDESys["DelVars"]
    );

    if
      (member(true, ParAndDelVar, 'i2'))
    then
      ListOfErrors := [op(ListOfErrors),
     cat(
       aDDESys["Parameters"][i2],
       ` is declared both parameter and delay variable `
     )
      ];
    end if; 


  #----------------------------------------------------------
  #
  # check if lists ExplicitAEs and others have common entries
  #
  #----------------------------------------------------------
    ExplVarsAndOtherVars1:=map(
      has,
      map(lhs,aDDESys["ExplicitAEs"]),
      aDDESys["DelVars"]
    );

    if
      (member(true, ExplVarsAndOtherVars1, 'i2'))
    then
      ListOfErrors := [op(ListOfErrors),
     cat(
       lhs(aDDESys["ExplicitAEs"][i2]),
       ` is declared in both ExplicitAEs and DelVars`
     )
      ];
    end if; 

   ExplVarsAndOtherVars2:=map(
      has,
      map(lhs,aDDESys["ExplicitAEs"]),
      map(lhs,aDDESys["Parameters"])
    );

    if
      (member(true, ExplVarsAndOtherVars2, 'i2'))
    then
      ListOfErrors := [op(ListOfErrors),
     cat(
       lhs(aDDESys["ExplicitAEs"][i2]),
       ` is declared in both ExplicitAEs and Parameters`
     )
      ];
    end if; 


   ExplVarsAndOtherVars3:=map(
      has,
      map(lhs,aDDESys["ExplicitAEs"]),
      aDDESys["DynVars"]
    );

    if
      (member(true, ExplVarsAndOtherVars3, 'i2'))
    then
      ListOfErrors := [op(ListOfErrors),
     cat(
       lhs(aDDESys["ExplicitAEs"][i2]),
       ` is declared in both ExplicitAEs and DynVars`
     )
      ];
    end if; 


  return(ListOfErrors); 

end proc; # ListOfErrorsInDAESysPart2

