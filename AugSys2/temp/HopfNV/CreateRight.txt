    ###============================================================
    # 
    # @path AugSys2/HopfNV/CreateRight.txt
    #
    # @param  
    #   a model to be augmented by general right eigenvector system
    #
    # @return  
    #   an AESys, first part is result from converting first arg
    #     to ODE-model using SubsExplicitAEsIntoODEs, 2nd part
    #     is eigenvector system
    #
    # @notes:
    #  (1) proc introduces variables fx[.,.]; proc checks whether
    #  fx already occurs in model and throws error if so
    # 
    # @revision
    #  2009-12-22 Moved from AugSys by dka
    #  020827 written by mmo, starting from corr. ComplexEigen2 file
    #
    ###============================================================
    CreateRight:=proc(
      aModel::DAESys
    )
  
      local ODEmodel, NumOfODEs, f_x, HopfSystemEquations, 
        HopfSystemVariables, NewEquations, NewVariables, 
        w1, w2, i1, HopfSysToBeSubs, ParsOfModel, VarsOfModel,
        SetOfODEmodelVars, Jac; 

      #
      # convert model to ODE model
      # 
        ODEmodel:= Aux:-SystemClasses:-subsExplicitAEsIntoDAESys(
          aModel
        );

      #
      # variable fx must not occur in ODEmodel
      #
        SetOfODEmodelVars:= {
          op(ODEmodel["DynVars"]), 
          op(ODEmodel["Parameters"]), 
          op(map(lhs, ODEmodel["ExplicitAEs"]))
        }; 

        if
          member(fx, SetOfODEmodelVars)
        then
          error("symbol fx must not occur in first argument");
        end if;

      #
      # prepare some vars needed repeatedly
      #
        NumOfODEs:= nops(ODEmodel["ODEs"]);
        VarsOfModel:= ODEmodel["DynVars"];
        ParsOfModel:= ODEmodel["Parameters"]; 
  
      #
      # fill jacobian f_x with names fx[1, 1]...fx[n, n]
      #
        f_x:= matrix(NumOfODEs, NumOfODEs,
          [seq(
            seq(
              fx[i1, i2],
              i2= 1..NumOfODEs
            ),
            i1= 1..NumOfODEs
          )]
        ); 
  
      #------------------------------------------------------------
      #
      # start construction of system equations:
      #
      #------------------------------------------------------------

      #
      # augmented system will be in HopfSysEquations, 
      # HopfSysVariables etc., 
      # lists NewEquations, NewVariables are used to build next block 
      #

        #--------------------
        #
        # model equations
        #
        #--------------------
          NewEquations:= [seq(
            0= rhs(ODEmodel["ODEs"][i1]),
            i1= 1..NumOfODEs
          )];
  
          NewVariables:= ODEmodel["DynVars"];

          #
          # HopfSystem with NewEquations, NewVariables
          #
            HopfSystemEquations:= NewEquations;
            HopfSystemVariables:= NewVariables; 

        #--------------------    
        #
        # equations which define entries of jacobian
        #
        #--------------------
       
        #
        # remember we need vectors to be represented by lists of
        # names for linalg and Aux:-TensProd
        #

          NewVariables:= [
            seq(
              seq(
                fx[i1, i2],
                i2= 1..NumOfODEs
              ),
              i1= 1..NumOfODEs
            )
          ];
       
          Jac:= Aux:-Derivs:-f_x(
            ODEmodel["ODEs"],
            ODEmodel["DynVars"]
          );

          NewEquations:= [
            seq(
              seq(
                0= fx[i1, i2]- Jac[i1, i2],
                i2= 1..NumOfODEs
              ),
              i1= 1..NumOfODEs
            )
          ]; 
    
          HopfSystemEquations:= [
            op(HopfSystemEquations),   
            op(NewEquations)                    
          ];
  
          HopfSystemVariables:= [
            op(HopfSystemVariables),
            op(NewVariables)
          ];
        
        #--------------------    
        #
        # eigenvector equations 0= f_x w1+ omega* w2 
        #
        #--------------------
       
        #
        # remember we need vectors to be represented by lists of
        # names for linalg and Aux:-TensProd
        #
          w1:= [seq(
            wvec1[i1], 
            i1=1..NumOfODEs)
          ]; 
          w2:= [seq(
            wvec2[i1], 
            i1=1..NumOfODEs)
          ];

          NewVariables:= [ 
            op(w1),
            op(w2),
            sigma,  
            omega
          ];
       
          NewEquations:= Aux:-TensProd:-Aij_xj(
              f_x, w1
          );
 
          NewEquations:= linalg[matadd](
            NewEquations,
            linalg[scalarmul](w2, omega)
          );

          NewEquations:=[seq(
            0=NewEquations[i1],
            i1=1..NumOfODEs
          )];
   
          HopfSystemEquations:= [
            op(HopfSystemEquations),   
            op(NewEquations)                    
          ];
  
          HopfSystemVariables:= [
            op(HopfSystemVariables),
            op(NewVariables)
          ];
        
        #--------------------
        #
        # eigenvector equations 0= f_x w2- omega* w1
        #
        #--------------------
           NewEquations:= Aux:-TensProd:-Aij_xj(
             f_x, w2
           ); 

           NewEquations:= linalg[matadd](
             NewEquations, 
             linalg[scalarmul](w1, -omega)
           ); 
  
           NewEquations:=[seq(
             0=NewEquations[i1],
             i1=1..NumOfODEs
           )];
  
          #
          # append new equations to HopfSystem
          #
            HopfSystemEquations:= [
              op(HopfSystemEquations),
              op(NewEquations)                    
            ];
    
        #--------------------  
        #
        # normalization: w1^T *w1 + w2^T *w2 -1 = 0  
        #
        #--------------------

          NewEquations:= [
            0= linalg[multiply](w1, w1)+ linalg[multiply](w2, w2)-1
          ];
  
          #
          # append new equation to HopfSystem
          #
            HopfSystemEquations:= [
              op(HopfSystemEquations),
              op(NewEquations)                    
            ];

        #--------------------  
        #
        # orthogonality: w1^T *w2 = 0  
        #
        #--------------------
          NewEquations:= [0=linalg[dotprod](w1, w2, 'orthogonal')];
  
          #
          # append new equation to HopfSystem
          #
            HopfSystemEquations:= [
              op(HopfSystemEquations),
              op(NewEquations)                    
            ];
                          
      #------------------------------------------------------------
      #
      # build table for augmented system from lists
      #
      #------------------------------------------------------------
        HopfSysToBeSubs:= table();
        HopfSysToBeSubs["Equations"]:= HopfSystemEquations;
        HopfSysToBeSubs["Variables"]:= HopfSystemVariables;
        HopfSysToBeSubs["Parameters"]:= ODEmodel["Parameters"];

$include <HopfNV/CreateRight/TemplateModule.txt>

    end proc;  # AugSys:-Hopf:-CreateRight
