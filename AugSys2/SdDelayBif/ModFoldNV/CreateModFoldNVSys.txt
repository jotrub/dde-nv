###########################################################################
# 
# @path AugSys2/SdDelayBif/ModFoldNV/CreateModFoldNVSys.txt
#
# @brief Creates an augmented NV system for Generalized critical boundaries
#        for DDESys with state dependent delay without algebraic equations
#
# @param  
#   a model (state dependent DDESys) to be augmented by system for finding normal vectors to generalized eigenvalue constraints,
#   list of names of free parameters Alpha
#   highest real part of eigenvalue sigma
#
# @notes
#    (i) DelVars in DDESys should contain N*M names of variables that correspond to the order: 
#        [var1(t-tau1),var2(t-tau1),...,varN(t-tau1),
#         var1(t-tau2),var2(t-tau2),...,varN(t-tau2),
#         ..., var1(t-tauM),var2(t-tauM),...,varN(t-tauM)],
#         where N=dim(DDESys["DynVars"]) and M is number of delays
#    (ii) We refer to delays as tau[j] and collect them in a vector of delays 
#        
#
# @authors dka, jot
#
# @return 
#     normal vector system for DDEs
# 
# @revision
#    2016-03-23 Created from a copy of SdDelayBif/ModHopfNV/CreateModHopfNVSys by jot 
# 
###########################################################################
    CreateModFoldNVSys:=proc(
      aSys::DDESys,
      ListOfNVparams::list(name),
      sigma::float
    )

      local ListofNames, item, ComplexRightEigSys, ExtendedSysToBeSubs,
     	ExtendedSystemEquations, ExtendedSystemVariables,
	ExtendedSystemParameters, DESys, A0, A0Transp, A0x,
	A0alpha, f_p, A0pTransp, V, U, R, NewVariables, NewEquations, 
        f_alpha,f_alphaTransp,W,fxx_w,v_fxx_w,
        fxalpha_w,v_fxalpha_w,
        NumNVparamsToBeSubs, ParsOfModel, VarsOfModel, result, 
        DynEqnsOfModel, NumOfEqnsOfModel, i2, NumOfDynEqnsInModule,
        listofDelVars, numberOfDelays, listofDelays,tau, Atau, AtauTransp, AtauX, AtauAlpha, 
        itTau, VarsDelTau, VVec, WVec, UVec, RVec,
        replacementForDelayVars, partialSum, delayTerm, j1, 
        A1x_w, v_A1x_w, 
        A1alpha_w,  v_A1alpha_w, tau_x, tau_alpha;
        

       #
       # make sure entries of ListOfNVparams exist in first arg
       #

        ListofNames:=map(lhs,aSys["Parameters"]);

        for item in ListOfNVparams do
          if not member(item,ListofNames)
	  then
	    error(
	      "requested normal vector parameter %1 does not exist in model",
	      item
	    );
	  end if;
	end do; 


       #
       # make sure that dimension of aSys["DelVars"] is equal to N*M
       #

       listofDelVars:=aSys["DelVars"];
       numberOfDelays:=nops(listofDelVars)/nops(aSys["DynVars"]);

       if not type(numberOfDelays,integer)
	 then
	   error(
	     "dimension of names for delayed variables \"DynVars\" which is equal to %1 should result from the number of delays multiplied by %2",
	      nops(listofDelVars),
	      nops(aSys["DynVars"])
	   );
       end if;


      #
      # make sure, that the number of entries in aSys["Delays"] is equal to number of Delays
      #
 
      listofDelays:=aSys["Delays"];                        # taking state dependency into account...

      if not evalb(nops(listofDelays)=numberOfDelays)
	 then
	   error("number of delays in DDE and number of elements in delay vector are not equal",nops(listofDelays),numberOfDelays);
       end if;
       
      #
      # assign the elements of listofDelays to the variables tau[]
      #      
       for j1 from 1 to numberOfDelays do

	    tau[j1]:=rhs(listofDelays[j1]);
      end do;
 
      #
      # substitude into DynEqns AlgEqns, prepare some variables
      # 
       
        DESys := Aux:-SystemClasses:-subsExplicitAEsIntoDAESys(aSys); 
        

        DynEqnsOfModel:= DESys["ODEs"]; 
        NumOfEqnsOfModel:= nops(DynEqnsOfModel);
        VarsOfModel:= DESys["DynVars"];
        ParsOfModel:= DESys["Parameters"]; 

      #
      # A0 equals df/dx(t)
      #
  
        A0:= Aux:-Derivs:-f_x(
          DynEqnsOfModel,
          VarsOfModel
        );

        A0Transp:= LinearAlgebra[Transpose](A0); 

        f_alpha:= Aux:-Derivs:-f_p(
          DynEqnsOfModel,
          ListOfNVparams
        );

        f_alphaTransp:= LinearAlgebra[Transpose](
          f_alpha
        ); 

        
        A0x:= Aux:-Derivs:-f_xx(
          DynEqnsOfModel,
          VarsOfModel
        ); 

        A0alpha:= Aux:-Derivs:-f_xp(
	  DynEqnsOfModel,
          VarsOfModel,
          ListOfNVparams
 	);         
 

      #
      # Atau[i] equals df/dx(t-tau[i])
      #

      for itTau from 1 to numberOfDelays do
        

        VarsDelTau:=[seq(listofDelVars[jTau],
                     jTau=NumOfEqnsOfModel*(itTau-1)+1..NumOfEqnsOfModel*(itTau))];

	
	Atau[itTau]:= Aux:-Derivs:-f_x(
          DynEqnsOfModel,
          VarsDelTau
        );

        AtauTransp[itTau]:= LinearAlgebra[Transpose](Atau[itTau]); 

        AtauX[itTau]:= Aux:-Derivs:-f_xp(
          DynEqnsOfModel,
          VarsDelTau,
          VarsOfModel
        ); 

        AtauAlpha[itTau]:= Aux:-Derivs:-f_xp(
	  DynEqnsOfModel,
          VarsDelTau,
          ListOfNVparams
 	);         

      end do;

      #
      # derivations of tau
      #
      
      for itTau from 1 to numberOfDelays do
      
        tau_x[itTau]:= Aux:-Derivs:-f_p(
          [tau[itTau]],
          VarsOfModel);
      
	tau_alpha[itTau]:= Aux:-Derivs:-f_p(
          [tau[itTau]],
          ListOfNVparams
        );
      
      end do;
      
      
    
      #------------------------------------------------------------
      #
      # start construction of normal vector system
      #
      #------------------------------------------------------------
        #
        # v, w
        #
          V:= [seq(
            v[i1], 
            i1=1..NumOfEqnsOfModel)
          ]; 

 
  
          W:= [seq(
            w[i1], 
            i1=1..NumOfEqnsOfModel)
          ]; 

          
        #
        # u and r 
        #
          U:= [seq(
          u[i1], 
            i1=1..NumOfEqnsOfModel)
          ]; 
  
          R:= [seq(
          r[i1], 
            i1=1..nops(ListOfNVparams))
          ]; 
  
          VVec:= convert(V,Vector);
          WVec:= convert(W,Vector);
          UVec:= convert(U,Vector);
          RVec:= convert(R,Vector);    

      
      #
      # augmented system will be in ExtendedSystemEquations, 
      # ExtendedSystemVariables etc., 
      # lists NewEquations, NewVariables are used to build next block 
      #

       #--------------------
       #
       # model equations f=0
       #
       #--------------------

          NewEquations:= [seq(
            0= rhs(DESys["ODEs"][i1]),
            i1= 1..nops(DESys["ODEs"])
          )];
          
          # replace delayed variables with steady states
          
          replacementForDelayVars:=[];

          for itTau from 1 to numberOfDelays do
              replacementForDelayVars:=[op(replacementForDelayVars),
	                              seq(listofDelVars[jTau]=VarsOfModel[ modp(jTau-1,NumOfEqnsOfModel)+1],
	                                 jTau=NumOfEqnsOfModel*(itTau-1)+1..NumOfEqnsOfModel*(itTau))];
          end do;

          NewEquations:=subs(replacementForDelayVars,NewEquations);

          NewVariables:= DESys["DynVars"];

          #
          # ExtendedSystem with NewEquations, NewVariables
          #
            ExtendedSystemEquations:= NewEquations;
            ExtendedSystemVariables:= NewVariables; 

        
        #----------------------------------------
        #
        # A0 w + A1 exp(-sigma tau) w - sigma w=0
        #
        #----------------------------------------

    
         partialSum:=Vector(NumOfEqnsOfModel);
         for j1 from 1 to numberOfDelays do
	    partialSum:= partialSum - (Atau[j1].WVec)*exp(-sigma*tau[j1]);
         end do;         
        
         NewEquations := + partialSum
			 - A0.WVec
			 + sigma*WVec;
         
         NewEquations:=[seq(0=NewEquations[i1],i1=1..NumOfEqnsOfModel)];
         NewEquations:=subs(replacementForDelayVars,NewEquations);

         NewVariables:=[op(W)];
          
          ExtendedSystemEquations:= [
            op(ExtendedSystemEquations),   
            op(NewEquations)                    
          ];
  
          ExtendedSystemVariables:= [
            op(ExtendedSystemVariables),
            op(NewVariables)
          ];

        
       

        #----------------------------------------
        #
        # w'w -1 =0
        #
        #----------------------------------------

        

       NewEquations := LinearAlgebra[Transpose](WVec).WVec-1;


       ExtendedSystemEquations:= [
            op(ExtendedSystemEquations),   
            0=NewEquations                    
          ];


        #----------------------------------------
        #
        # A0^T v + A1^T v exp(-sigma tau) - sigma v + g w =0
        #
        #----------------------------------------

        partialSum:=Vector(NumOfEqnsOfModel);
        for j1 from 1 to numberOfDelays do
	    partialSum:=partialSum
	                -(AtauTransp[j1].VVec)*exp(-sigma*tau[j1]);
        end do;

         NewEquations := + partialSum
			 - A0Transp.VVec 
                         + sigma*VVec
                         + 2*WVec*g1;
         
         NewEquations:=[seq(0=NewEquations[i1],i1=1..NumOfEqnsOfModel)];
         NewEquations:=subs(replacementForDelayVars,NewEquations);

         NewVariables:=[op(V),g1];
          
          ExtendedSystemEquations:= [
            op(ExtendedSystemEquations),   
            op(NewEquations)                    
          ];
  
          ExtendedSystemVariables:= [
            op(ExtendedSystemVariables),
            op(NewVariables)
          ];


    
        #----------------------------------------
        #
        # v^T w  -1 =0 
        #
        #
        #----------------------------------------

#   partialSum:=0;
#     for j1 from 1 to numberOfDelays do
#	    partialSum:=partialSum
#                        +(LinearAlgebra[Transpose](VVec).(Atau[j1].WVec))*exp(-sigma*tau[j1])*tau[j1];
#        end do; 
#        
 #       NewEquations := + LinearAlgebra[Transpose](VVec).WVec # hier vorzeichen geändert 150824 jot#
#			+ partialSum
#                        - 1;

  #      NewEquations:=subs(replacementForDelayVars,NewEquations);
#
 #       ExtendedSystemEquations:= [
  #          op(ExtendedSystemEquations),   
   #         0=NewEquations                    
    #      ];
  
         
       

        #----------------------------------------
        #
        # A0^T u + v^T A0x w+ + v^T A1x w exp(-sigma tau)  =0
	#
        #----------------------------------------
          fxx_w:= Aux:-TensProd:-Tijk_xj(
            A0x, W
          );

          v_fxx_w:= Aux:-TensProd:-xi_Aij(
            V,
            fxx_w
          ); 
          
          partialSum:=Vector(NumOfEqnsOfModel);
          
          for j1 from 1 to numberOfDelays do
	     
	     A1x_w[j1]:= Aux:-TensProd:-Tijk_xj(AtauX[j1], W);
	     
	     v_A1x_w[j1]:= Aux:-TensProd:-xi_Aij(V,A1x_w[j1]);

	    partialSum:=partialSum
			 + AtauTransp[j1].UVec # Ableitungen eingeführt 150824 jot 
	                 - convert(v_A1x_w[j1],Vector)*exp(-sigma*tau[j1])
	                 + LinearAlgebra[Transpose](sigma*(LinearAlgebra[Transpose](VVec)).(convert(Atau[j1].WVec,Matrix).tau_x[j1])*exp(-sigma*tau[j1]));
	                 # jot2jot: Check product Atau.Wvec
	  end do; 

          
          NewEquations:= + partialSum
			 + A0Transp.UVec 
                         - convert(v_fxx_w,Vector);

          NewEquations:=[seq(0=NewEquations[i1],i1=1..NumOfEqnsOfModel)];
          NewEquations:=subs(replacementForDelayVars,NewEquations);   
          
          NewVariables:= U;
          
          ExtendedSystemEquations:= [
            op(ExtendedSystemEquations),   
            op(NewEquations)                    
          ];
  
          ExtendedSystemVariables:= [
            op(ExtendedSystemVariables),
            op(NewVariables)
          ];

        #----------------------------------------
        #
        # f_alpha^T u + v^T A0alpha w + 
        #             + v^T A1alpha w exp(-sigma tau) 
        #             - sigma*tau_alpha*v^T A1 w exp(-sigma tau)  - r =0
        #
        #----------------------------------------
          
          
          fxalpha_w:= Aux:-TensProd:-Tijk_xj(
            A0alpha, W
          );

          v_fxalpha_w:= Aux:-TensProd:-xi_Tij(
            V,
            fxalpha_w
          ); 

          partialSum:=Vector(nops(ListOfNVparams));
          
          for j1 from 1 to numberOfDelays do
	     
	     A1alpha_w[j1]:= Aux:-TensProd:-Tijk_xj(AtauAlpha[j1], W);
	     
	     v_A1alpha_w[j1]:= Aux:-TensProd:-xi_Tij(V,A1alpha_w[j1]);
	
	     partialSum:= partialSum
	                 - convert(v_A1alpha_w[j1],Vector)*exp(-sigma*tau[j1])
	                 + LinearAlgebra[Transpose](sigma*(LinearAlgebra[Transpose](VVec)).(convert(Atau[j1].WVec,Matrix).tau_alpha[j1])*exp(-sigma*tau[j1]));
          end do; 


          NewEquations := + partialSum
                          + f_alphaTransp.UVec      	
                          - convert(v_fxalpha_w,Vector) 
                          - RVec;
                        
                        

          
                         
          NewEquations:=[seq(0=NewEquations[i1],i1=1..nops(ListOfNVparams))];
          NewEquations:=subs(replacementForDelayVars,NewEquations);   

          NewVariables:= R; 


           ExtendedSystemEquations:= [
            op(ExtendedSystemEquations),   
            op(NewEquations)                    
          ];


           ExtendedSystemVariables:= [
            op(ExtendedSystemVariables),
            op(NewVariables)
          ];
      
      
	#----------------------------------------
        #
        # r^T r  -1 =0 
        #
        #
        #----------------------------------------

      
      
       NewEquations := LinearAlgebra[Transpose](RVec).RVec-1;


       ExtendedSystemEquations:= [
            op(ExtendedSystemEquations),   
            0=NewEquations                    
          ];

      
      
      #------------------------------------------------------------
      #
      # build table for augmented system from lists
      #
      #------------------------------------------------------------
      
      #
      # apart from vars listed explicitly, the following vars
      # are passed on to template:
      #   A0 -> Jac
      #   NumOfEqnsOfModel -> NumDynEqns
      #
        ExtendedSysToBeSubs:= table();
        ExtendedSysToBeSubs["Equations"]:= ExtendedSystemEquations;
        ExtendedSysToBeSubs["Variables"]:= ExtendedSystemVariables;
        ExtendedSysToBeSubs["Parameters"]:= [op(DESys["Parameters"])];
    
        NumNVparamsToBeSubs:= nops(ListOfNVparams); 

$include <SdDelayBif/ModFoldNV/CreateModFoldNVSys/TemplateModule.txt>

    return result;

       end proc;  #AugSys2:-SdDelayBif:-ModFoldNV:-CreateModFoldNVSys