###############################################################################
#
# @path AugSys2/ConstraintInitializer/calcClosestCriticalPoint.txt
#
# @brief calcutate a closest point on the critical boundaries
#        for given fix point
#
# @notes NLP has to be defined before
#
# @revision
# 2008-03-14 written by dka
#
###############################################################################
calcClosestCriticalPoint:= proc()

  local anNLP, ListOfErrorsInAugSysNLP, NPSOLproc, StrartingCriticalPoint, Inform,
        Objf, OptPoint, isCalcClosestCPSucceeded;

  #
  # set isCalcClosestCPSucceeded to false
  # 
  
  isCalcClosestCPSucceeded:=false;
  AugSys2:-ConstraintInitializer:-CriticalPoint:-setCalcClosestCPSucceeded(isCalcClosestCPSucceeded);

  #
  # make sure that NLP is defined correct
  #

  anNLP:=AugSys2:-ConstraintInitializer:-NLP:-getNLP();
  ListOfErrorsInAugSysNLP:=Aux:-SystemClasses:-listOfErrorsInNLP(anNLP, 'strict');
  
  if (ListOfErrorsInAugSysNLP <> true)
  then error("NLP is not defined correct",ListOfErrorsInAugSysNLP);
  end if;

  #
  # check if the folder tmp already exists
  #
 
   if Aux:-FileOperations:-dirExists("tmp")
      then
        error("temporary directory ./tmp needed to run test already exists")
      else  
          mkdir("tmp")
      end if;

  #
  # check if starting point for optimization is defined
  #
  
  StrartingCriticalPoint:=AugSys2:-ConstraintInitializer:-CriticalPoint:-getStartingCriticalPoint();
  
  if not type(StrartingCriticalPoint,list(EvalsToFloat))
  then error("Starting critical point is not defined");
  end if;

  printf("\n");
  printf("running Optimization for NLP\n"); 

  NPSOLproc := NPSOL:-CreateInstance(anNLP, "./tmp");
  NPSOLproc:-setXVEC(StrartingCriticalPoint);
  NPSOLproc:-runOpt();
  
  Inform := NPSOLproc:-getInform();

  #
  # set isCalcClosestCPSucceeded to frue if optimization succeded
  # and Objf,OptPoint its values
  # 

  if (Inform=0)
  then
      isCalcClosestCPSucceeded:=true; 
      AugSys2:-ConstraintInitializer:-CriticalPoint:-setCalcClosestCPSucceeded(isCalcClosestCPSucceeded);
      Objf:= NPSOLproc:-getObjf();
      OptPoint:= NPSOLproc:-getXVEC();
      AugSys2:-ConstraintInitializer:-CriticalPoint:-setCalcClosestCPObjf(Objf);
      AugSys2:-ConstraintInitializer:-CriticalPoint:-setCalcClosestCPOptPoint(convert(OptPoint,list));
  end if;


  return(isCalcClosestCPSucceeded);

end proc; 

 
 
