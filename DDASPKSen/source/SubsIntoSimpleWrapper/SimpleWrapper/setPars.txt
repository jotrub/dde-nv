######################################################################
#
# @path DDASPKSen/SubsIntoSimpleWrapper/SimpleWrapper/setPars.txt
#
# @brief exported procedure setPars
#
######################################################################
      setPars:= proc(
        NewPars::list(EvalsToFloat)
      )
        local ListOfParamNames; 

        #
        # make sure that correct number of parameters has been passed
        #
          if not
            nops(NewPars)= NumParsInDAEsys
          then
            error("DAE system has %1 parameters", NumParsInDAEsys);
          end if; 

        #
        # set parameters to requested values
        #
          rpar_vec:= Vector(
            1..NumParsInDAEsys, map(evalf, NewPars), datatype= float[8]
          );  

        #
        # set parameters in Sys[Parameters]
        #
        # remember values in Sys[Parameters] are not used in 
        # calculations; substitution is only done to avoid confusion
        # since user can access Sys[Parameters] via GetSys() and
        # values used in calculation via GetPars, GetParameters
        #
          ListOfParamNames:= map(lhs, Sys["Parameters"]); 

          Sys["Parameters"]:= [seq(
            ListOfParamNames[i1]= NewPars[i1],
            i1= 1..nops(ListOfParamNames)
          )]; 

        return();
            
      end proc; # setPars

