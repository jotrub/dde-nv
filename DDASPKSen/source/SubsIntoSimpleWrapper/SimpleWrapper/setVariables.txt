######################################################################
#
# @path DDASPKSen/SubsIntoSimpleWrapper/SimpleWrapper/setVariables.txt
#
# @brief extracts var values from named list and
#   sets them for future calculations
#
# @param list of substitutions, type name= EvalsToFloat
#   list must contain values for all variables, may contain
#   other entries which will be ignored
#
# @return none, side effects only
#
# @notes set variables in instance to requested values
#
# @revision
# 020417 written by mmo
#
######################################################################
      setVariables:= proc(
        NewVars::list(name= EvalsToFloat)
      )

        local AssignedVars, MissingAssigns, VarNamesOfInstance,
          ListOfVarValues; 

        #
        # all parameters must be assinged by first argument
        #
          AssignedVars:= map(lhs, NewVars);
          VarNamesOfInstance:= [  # see SubsStandardNotation for
            op(Sys["DynVars"]),     # order of DynVars, AlgVars
            op(Sys["AlgVars"])
          ];  

          MissingAssigns:= `minus`(
            convert(VarNamesOfInstance, set),
            convert(AssignedVars, set)
          ); 

          if not
            MissingAssigns= {}
          then
            error(cat(
              "all variables must be assigned by first argument, "
              "variables %1 are missing", MissingAssigns
            ));
          end if;

        #
        # create a list of var values in the correct order and
        # call SetVars to do the actual assignment
        #         
          ListOfVarValues:= subs(
            NewVars,
            VarNamesOfInstance
          ); 
          SetVars(ListOfVarValues);
        
        return();

      end proc; # SetVariables

